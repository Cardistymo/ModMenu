plugins {
	id "com.modrinth.minotaur" version '1.1.0'
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name 'legacy-fabric'
		url 'https://maven.legacyfabric.net'
	}
	maven {
		name 'Jitpack'
		url 'https://jitpack.io'
	}
}

loom {
	setIntermediaryUrl(
		'https://maven.legacyfabric.net/net/fabricmc/intermediary/%1$s/intermediary-%1$s-v2.jar'
	)
}

dependencies {
	implementation "com.google.guava:guava:$guava_version"
	minecraft "com.mojang:minecraft:$minecraft_version"
	mappings "net.fabricmc:yarn:$yarn_mappings:v2"
	modApi "net.fabricmc:fabric-loader:$loader_version"

	modImplementation include("net.legacyfabric.legacy-fabric-api:legacy-fabric-api:1.2.0+1.12.2") {
		exclude( group: "net.legacyfabric.legacy-fabric-api", module: "legacy-fabric-lifecycle-events-v1" )
		exclude( group: "net.legacyfabric.legacy-fabric-api", module: "legacy-fabric-entity-events-v1" )
		exclude( group: "net.legacyfabric.legacy-fabric-api", module: "legacy-fabric-vanilla-command-api-v1" )
	}
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

tasks.withType(JavaCompile).configureEach {
	it.sourceCompatibility = 8 // for the IDE support
	it.options.encoding = 'UTF-8'
	it.options.release.set(8)
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

jar {
	from 'LICENSE'
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		if (project.hasProperty('legacy_fabric_maven_publish_token')) {
			maven {
				url 'https://maven.legacyfabric.net/'
				credentials {
					username 'BobTheBuildSlave'
					password project.getProperty('legacy_fabric_maven_publish_token')
				}
				authentication {
					basic(BasicAuthentication)
				}
			}
		}
	}
}

task modrinth(type: com.modrinth.minotaur.TaskModrinthUpload, dependsOn: remapJar) {
	onlyIf {
		project.hasProperty('modrinth_token')
	}

	token = project.hasProperty('modrinth_token') ? project.getProperty('modrinth_token') : ""
	projectId = "XzTYkVLx"
	versionNumber = project.version
	versionName = "[1.8.9] Legacy Mod Menu ${project.version}"
	releaseType = "release"

	uploadFile = file("${project.buildDir}/libs/${archivesBaseName}-${version}.jar")

	addGameVersion('1.8.9')
	addLoader('fabric')
}
